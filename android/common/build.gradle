plugins {
  id "org.jetbrains.kotlin.multiplatform"
  id "org.jetbrains.kotlin.native.cocoapods"
  id "com.android.library"
  id "kotlinx-serialization"
}

apply plugin: 'kotlinx-serialization'

version = "1.0.0-SNAPSHOT"

android {
  compileSdkVersion rootProject.ext.compileSdkVersion
  defaultConfig {
    minSdkVersion rootProject.ext.minSdkVersion
    targetSdkVersion rootProject.ext.targetSdkVersion
    versionCode 14
    versionName "1.0.0"
    testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }
  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  buildTypes {
    release {
      minifyEnabled false
    }

    debug {
      // MPP libraries don't currently get this resolution automatically
      matchingFallbacks = ['release']
    }
  }
}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
  sourceCompatibility = JavaVersion.VERSION_1_8
  targetCompatibility = JavaVersion.VERSION_1_8

  kotlinOptions {
    jvmTarget = '1.8'
    apiVersion = '1.3'
    languageVersion = '1.3'
  }
}

kotlin {
  targets {
    fromPreset(presets.android, 'jvm')

    // Change to `presets.iosArm64` to deploy the app to iPhone
    final def iOSTarget = System.getenv('SDK_NAME')?.startsWith('iphoneos')
        ? presets.iosArm64 : presets.iosX64
    fromPreset(iOSTarget, 'ios') {
      binaries.framework('Common')  {
        freeCompilerArgs.add("-Xobjc-generics")
      }
    }
  }
  sourceSets {
    commonMain {
      dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib"
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-common:$coroutines_version"
        implementation "org.jetbrains.kotlinx:atomicfu-common:$atomicfu_version"
      }
    }
    commonTest {
      dependencies {
        implementation "org.jetbrains.kotlin:kotlin-test-common:$kotlin_version"
        implementation "org.jetbrains.kotlin:kotlin-test-annotations-common:$kotlin_version"
      }
    }
    jvmMain {
      dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
        implementation "org.jetbrains.kotlinx:atomicfu:$atomicfu_version"
        api "org.reactivestreams:reactive-streams:1.0.2"
      }
    }
    jvmTest {
      dependencies {
        implementation "org.jetbrains.kotlin:kotlin-test-junit:$kotlin_version"
      }
    }
    iosMain {
      dependencies {
        implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core-native:$coroutines_version"
        implementation "org.jetbrains.kotlinx:atomicfu-native:$atomicfu_version"
      }
    }
  }

  cocoapods {
    summary = "Common code for 360Conferences conference app"
    homepage = "https://360conferences.com/"
  }
}

configurations {
  compileClasspath
}