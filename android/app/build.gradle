apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

def event_id = "chicagoroboto-2017"
def event_name = "Chicago Roboto"
def app_name = "Roboto"

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.2'
    defaultConfig {
        applicationId "com.chicagoroboto"
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 9
        versionName "1.7"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        buildConfigField "String", "EVENT_ID", "\"$event_id\""
        resValue "string", "app_name", "$app_name"
        resValue "string", "event_name", "$event_name"
    }
    if (hasProperty('CHICAGO_ROBOTO_STORE_FILE')) {
        signingConfigs {
            release {
                storeFile file(CHICAGO_ROBOTO_STORE_FILE)
                storePassword CHICAGO_ROBOTO_STORE_PASSWORD
                keyAlias CHICAGO_ROBOTO_KEY_ALIAS
                keyPassword CHICAGO_ROBOTO_KEY_PASSWORD
            }
        }
    }
    buildTypes {
        release {
            if (hasProperty('CHICAGO_ROBOTO_STORE_FILE')) {
                signingConfig signingConfigs.release
            }
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
}

kapt {
    generateStubs = true
}

dependencies {
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:design:25.3.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:recyclerview-v7:25.3.1'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "com.google.android.gms:play-services-maps:${rootProject.gpsVersion}"
    compile "com.google.firebase:firebase-core:${rootProject.gpsVersion}"
    compile "com.google.firebase:firebase-database:${rootProject.gpsVersion}"
    compile "com.google.firebase:firebase-auth:${rootProject.gpsVersion}"

    compile project(':core')

    compile 'com.github.bumptech.glide:glide:3.7.0'
    compile 'com.mikhaellopez:circularimageview:3.0.2'

    compile 'com.google.dagger:dagger:2.4'
    kapt 'com.google.dagger:dagger-compiler:2.4'
    provided 'org.glassfish:javax.annotation:10.0-b28'

    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    testCompile 'junit:junit:4.12'
    compile 'com.android.support:cardview-v7:25.3.1'
}

apply plugin: 'com.google.gms.google-services'