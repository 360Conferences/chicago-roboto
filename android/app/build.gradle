apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def event_id = "chicagoroboto-2018"
def event_name = "Chicago Roboto"
def app_name = "Roboto"

android {
    compileSdkVersion rootProject.ext.compileSdkVersion
    defaultConfig {
        applicationId "com.chicagoroboto"
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion
        versionCode 13
        versionName "2.2"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        buildConfigField "String", "EVENT_ID", "\"$event_id\""
        resValue "string", "app_name", "$app_name"
        resValue "string", "event_name", "$event_name"
    }
    if (project.hasProperty('CHICAGO_ROBOTO_STORE_FILE')) {
        signingConfigs {
            release {
                storeFile file(CHICAGO_ROBOTO_STORE_FILE)
                storePassword CHICAGO_ROBOTO_STORE_PASSWORD
                keyAlias CHICAGO_ROBOTO_KEY_ALIAS
                keyPassword CHICAGO_ROBOTO_KEY_PASSWORD
            }
        }
    }
    buildTypes {
        release {
            if (project.hasProperty('CHICAGO_ROBOTO_STORE_FILE')) {
                signingConfig signingConfigs.release
            }
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
        androidTest.java.srcDirs += 'src/androidTest/kotlin'
    }
}

dependencies {
  implementation libs.constraintLayout
  implementation libs.circularImageView
  implementation libs.firebaseAuth
  implementation libs.firebaseCore
  implementation libs.firebaseDatabase
  implementation libs.firebaseStorage
  implementation libs.kotlinStdlib
  implementation libs.multilineToolbar
  implementation libs.playServicesMaps
  implementation libs.recyclerView
  implementation libs.supportAppCompat
  implementation libs.supportCardView
  implementation libs.supportDesign

  implementation project(':core')

  implementation libs.glide

  implementation libs.dagger
  kapt libs.daggerCompiler
  compileOnly 'org.glassfish:javax.annotation:10.0-b28'

  androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
      exclude group: 'com.android.support', module: 'support-annotations'
  })

  testImplementation libs.junit4
}

apply plugin: 'com.google.gms.google-services'